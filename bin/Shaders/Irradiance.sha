#shader vertex
#version 130

in vec3 vertexPos;

out vec3 fragmentCoord;

uniform mat4 projection;
uniform mat4 view;

void main()
{
    vec4 worldPos = vec4(vertexPos, 1);
    gl_Position = projection * view * worldPos;

    fragmentCoord = normalize(worldPos.xyz);
}

#shader fragment
#version 130

in vec3 fragmentCoord;

out vec4 outColor;

uniform samplerCube image;

const float PI = 3.14159265359;

void main()
{
    vec3 irradiance = vec3(0.0);

    // tangent space calculation from origin point
    vec3 up    = vec3(0.0, 1.0, 0.0);
    vec3 right = cross(up, fragmentCoord);
    up         = cross(fragmentCoord, right);

    float sampleDelta = 0.025;
    float nrSamples = 0.0;

    for(float phi = 0.0; phi < 2.0 * PI; phi += sampleDelta)
    {
        for(float theta = 0.0; theta < 0.5 * PI; theta += sampleDelta)
        {
            // spherical to cartesian (in tangent space)
            vec3 tangentSample = vec3(sin(theta) * cos(phi),  sin(theta) * sin(phi), cos(theta));
            // tangent space to world
            vec3 sampleVec = tangentSample.x * right + tangentSample.y * up + tangentSample.z * fragmentCoord;

            irradiance += texture(image, sampleVec).rgb * cos(theta) * sin(theta);
            nrSamples++;
        }
    }
    irradiance = PI * irradiance * (1.0 / float(nrSamples));

    outColor = vec4(irradiance, 1.0);
}
